{ pkgs, ... }:

let
  json = pkgs.formats.json { };

  # For things that should only apply to Emacs
  emacsIfCondition = {
    type = "frontmost_application_if";
    bundle_identifiers = [
      "^org\\.gnu\\.Emacs$"
      "^org\\.gnu\\.Emacs-.*$"
      "^net\\.mitsuya\\.Emacs$"
    ];
  };

  disableShortcut = from: {
    type = "basic";
    inherit from;
    to = [ { key_code = "vk_none"; } ];
    conditions = [ emacsIfCondition ];
  };

  # macOS shortcuts to disable (note that this is affected by how we swap ctrl and cmd)
  # This was generated by ChatGPT from a diff of com.apple.symbolichotkeys.plist files, so could be wrong
  shortcuts = [
    # Spotlight
    {
      key_code = "spacebar";
      modifiers = {
        mandatory = [ "control" ];
      };
    }
    {
      key_code = "spacebar";
      modifiers = {
        mandatory = [
          "control"
          "option"
        ];
      };
    }

    # Mission Control / App windows / Move between Spaces
    {
      key_code = "up_arrow";
      modifiers = {
        mandatory = [ "command" ]; # TODO control or command?
      };
    }
    {
      key_code = "down_arrow";
      modifiers = {
        mandatory = [ "command" ]; # TODO control or command?
      };
    }
    {
      key_code = "left_arrow";
      modifiers = {
        mandatory = [ "command" ]; # TODO control or command?
      };
    }
    {
      key_code = "right_arrow";
      modifiers = {
        mandatory = [ "command" ]; # TODO control or command?
      };
    }

    # Dock toggle
    {
      key_code = "d";
      modifiers = {
        mandatory = [
          "control"
          "option"
        ];
      };
    }

    # Help menu
    {
      key_code = "slash";
      modifiers = {
        mandatory = [
          "control"
          "shift"
        ];
      };
    }

    # Quick Note (Fn-Q / Globe-Q)
    {
      key_code = "q";
      modifiers = {
        mandatory = [ "fn" ];
      };
    }
  ];

  # For things that should NOT apply to certain terminals
  terminalsUnlessCondition = {
    type = "frontmost_application_unless";
    bundle_identifiers = [
      "^com\\.mitchellh\\.ghostty$"
      "^net\\.kovidgoyal\\.kitty$"
    ];
  };

  swapModifiers = pair: {
    type = "basic";
    from = {
      key_code = pair.from;
    };
    to = [ { key_code = pair.to; } ];
    conditions = [ terminalsUnlessCondition ];
  };

  modifierPairs = [
    {
      from = "left_command";
      to = "left_control";
    }
    {
      from = "left_control";
      to = "left_command";
    }
    {
      from = "right_command";
      to = "right_control";
    }
    {
      from = "right_control";
      to = "right_command";
    }
  ];

  allKeys = [
    "a"
    "b"
    "c"
    "d"
    "e"
    "f"
    "g"
    "h"
    "i"
    "j"
    "k"
    "l"
    "m"
    "n"
    "o"
    "p"
    "q"
    "r"
    "s"
    "t"
    "u"
    "v"
    "w"
    "x"
    "y"
    "z"
    "1"
    "2"
    "3"
    "4"
    "5"
    "6"
    "7"
    "8"
    "9"
    "0"
    "spacebar"
    "return_or_enter"
    "escape"
    "delete_or_backspace"
    "tab"
    "hyphen"
    "equal_sign"
    "open_bracket"
    "close_bracket"
    "backslash"
    "semicolon"
    "quote"
    "grave_accent_and_tilde"
    "comma"
    "period"
    "slash"
    "up_arrow"
    "down_arrow"
    "left_arrow"
    "right_arrow"
  ];

  fixTripleModifier = key: {
    type = "basic";
    from = {
      key_code = key;
      modifiers = {
        mandatory = [
          "control"
          "option"
          "shift"
        ];
      };
    };
    to = [
      {
        key_code = key;
        modifiers = [
          "command"
          "option"
          "shift"
        ];
      }
    ];
    conditions = [ emacsIfCondition ];
  };

  karabiner = {
    profiles = [
      {
        name = "Default profile";
        selected = true;
        virtual_hid_keyboard = {
          keyboard_type_v2 = "iso";
        };

        complex_modifications = {
          rules = [
            {
              description = "Swap cmd and ctrl (except in ghostty and kitty)";
              manipulators = builtins.map swapModifiers modifierPairs;
            }

            {
              manipulators = [
                {
                  description = "Change caps_lock to command+control+option+shift.";
                  type = "basic";
                  from = {
                    key_code = "caps_lock";
                    modifiers = {
                      optional = [ "any" ];
                    };
                  };
                  to = [
                    {
                      key_code = "left_shift";
                      modifiers = [
                        "left_command"
                        "left_control"
                        "left_option"
                      ];
                    }
                  ];
                }
              ];
            }

            {
              description = "Disable various macOS shortcuts in Emacs";
              manipulators = builtins.map disableShortcut shortcuts;
            }

            {
              description = "Fix Control-Option-Shift combinations in Emacs";
              manipulators = builtins.map fixTripleModifier allKeys;
            }
          ];
        };
      }
    ];
  };
in
{
  xdg.configFile."karabiner/karabiner.json".source = json.generate "karabiner.json" karabiner;
}
